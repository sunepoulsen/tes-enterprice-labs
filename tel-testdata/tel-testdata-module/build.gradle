import org.apache.tools.ant.filters.*

plugins {
    id 'groovy'
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'jacoco'
    id 'com.google.cloud.tools.jib'
}

dependencies {
    // TES Enterprise Labs
    implementation project(':tel-testdata:tel-testdata-integrations')

    // Tech Easy Solutions
    implementation 'dk.sunepoulsen.tes-foundation:tes-spring-boot-backend-logging:2.0.0-SNAPSHOT'
    implementation 'dk.sunepoulsen.tes-foundation:tes-rest-models:2.0.0-SNAPSHOT'
    implementation 'dk.sunepoulsen.tes-foundation:tes-spring-boot-rest-exceptions:2.0.0-SNAPSHOT'

    // Spring Boot
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // Liquibase
    implementation 'com.h2database:h2:2.2.224'
    implementation 'org.liquibase:liquibase-core:4.27.0'

    // Database Drivers
    implementation 'org.postgresql:postgresql:42.7.3'
}

jib {
    from {
        image = "${project.property('backend.module.base.image.name')}:${project.property('backend.module.base.image.tag')}"
    }
    to {
        image = project.property('tel-testdata.docker.image.name')
        tags = [project.property('version')]
    }

    container {
        jvmFlags = [
                '-Dcom.sun.management.jmxremote',
                '-Dcom.sun.management.jmxremote.port=9010',
                '-Dcom.sun.management.jmxremote.local.only=false',
                '-Dcom.sun.management.jmxremote.authenticate=false',
                '-Dcom.sun.management.jmxremote.ssl=false'
            ]
        ports = ['8080', '9010']
    }
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PRIVATE
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

tasks.build.dependsOn tasks.jibDockerBuild
